module Game

open "UnityEngine.dll"
open UnityEngine

worldEntity World = {
  Landscapes : [Landscape]
  Jeep : Truck

  rule Landscapes  = 
    let a =
      [for e in Landscapes do
       where e.Checkpoint.isEntered
       select e]
    if a.Count > 0 then
      let headOfA = a.Head
      let headOfATransform = headOfA.transform
      let headOfAPosition = headOfATransform.position
      yield new Landscape(new Vector3(0.0f,0.0f,headOfAPosition.z - 240.0f)) :: Landscapes

  rule Landscapes =
    let destroyed_filter =
      [for e in Landscapes do
       where (not e.Destroyed)
       select e]
    yield destroyed_filter

  Create() =
    {
     Jeep = new Truck()
     Landscapes = [new Landscape(new Vector3(0.0f,0.0f,0.0f));new Landscape(new Vector3(0.0f,0.0f,-80.0f));new Landscape(new Vector3(0.0f,0.0f,-160.0f))]
    }
}

entity Landscape = {
  inherit UnityLandscape
  Zombies	: [Zombie]
  SP : [Transform]
  hasSpawned : bool
  DestroyDistance : float

  rule Zombies = 
    [for a in Zombies do
	   where (not a.Destroyed)
	   select a]

  rule Zombies, hasSpawned = 
    wait (not hasSpawned)
    let SPHP = SP.Head.position 
    yield new Zombie(SPHP) :: Zombies, true

  rule Checkpoint.isEntered =
    wait Checkpoint.isEntered
    yield Checkpoint.isEntered
    yield false

  //rule Destroyed =
    //wait Vector3.Distance(Position,world.Jeep.Position) > DestroyDistance
    //yield true

  rule SP = 
    [for a in Spawnpoints2 do
	   select a]

  Create(StartP : Vector3) = {
  Base = UnityLandscape.Instantiate(StartP)
  Zombies = []
  SP = []
  hasSpawned = false
  DestroyDistance = 500.0f
  }
}

entity Truck = {
   inherit TruckScript

   Velocity : Vector3
   AxleInfos : [AxleInfo]
   maxMotorTorque : float
   maxSteeringAngle : float
   motor : float
   steering : float

   rule CenterOfMass =
     yield new Vector3(0.2f,0.0f,-1.0f)

   Create()=
    {
      motor = 0.0f
      steering = 0.0f
      maxMotorTorque = 250.0f // Max speed of car
      maxSteeringAngle = 50.0f // Max steering angle of car
      AxleInfos = [new AxleInfo(leftFrontWheel,rightFrontWheel,true,true);new AxleInfo(leftRearWheel,rightRearWheel,true,false)] //
      Velocity = Vector3(0.0f, 0.0f, 0.0f)
      Base = TruckScript.Instantiate()
    }
}

entity AxleInfo = {

  leftWheel : WheelCollider
  rightWheel : WheelCollider
  motor : bool
  steering : bool
  
  rule leftWheel.steerAngle =
    wait steering
    let steeringAngle = world.Jeep.maxSteeringAngle * Input.GetAxis("Horizontal")
    yield steeringAngle

  rule rightWheel.steerAngle =
    wait steering
    let steeringAngle = world.Jeep.maxSteeringAngle * Input.GetAxis("Horizontal")
    yield steeringAngle

  rule leftWheel.motorTorque =
    wait motor
    let speed = world.Jeep.maxMotorTorque * Input.GetAxis("Vertical") * -1.0f
    yield speed

  rule rightWheel.motorTorque =
    wait motor
    let speed = world.Jeep.maxMotorTorque * Input.GetAxis("Vertical") * -1.0f
    yield speed

  Create(lW : WheelCollider, rW : WheelCollider, m : bool, s: bool)=
  {
    leftWheel = lW
    rightWheel = rW
    motor = m
    steering = s
  }
}

entity Zombie = {
  inherit UnityZombie
  JeepPos : Vector3

  rule Position, speed, Destroyed = 
    if Destroyed = false then
      if Position = JeepPos then // collision
        yield new Vector3(0.0f,0.0f,0.0f), 0.0f, false
      else // not in range
        yield Position, 1.0f*dt, false

  rule currenta,targeta = Position,  JeepPos

  rule Rotation, Destroyed = 
    if Destroyed = false then
      if Position = JeepPos then
        yield new Quaternion(0.0f,0.0f,0.0f,0.0f), false
      else
        yield Rotation, false

  rule JeepPos = world.Jeep.Position

  //rule collision =
    //if Vector3.Distance(world.Jeep.Position,Position) < 4.0f then
      //yield true
    //else
      //yield false

  rule shot, Destroyed = 
    if OnMouseOver then
      if Input.GetMouseButtonDown(0) then
        yield true, false
        wait 2.0f
        yield false, true

  Create(pos : Vector3) = 
  {
    JeepPos = pos
    UnityZombie = UnityZombie.Instantiate(pos)
  }
}
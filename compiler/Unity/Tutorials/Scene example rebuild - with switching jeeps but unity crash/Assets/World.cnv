module Game

open "UnityEngine.dll"
open UnityEngine

worldEntity World = {
  inherit UnityLandscape
  Landscapes : [Landscape]
  Jeep : Option<Truck>
  Zombies : [Zombie] // are created in the landscape in groups
  Pistols : [Gun]
  Flashs : [Light]
  GUIpanel : GUI
  Health : float
  ActiveBoR : string
  Counter : int
  Gasstations : [Gasstation]

  rule Zombies = 
    let groups = 
      [for a in Landscapes do
       for b in a.Group do
       select b]
    let zombiegroup = 
      [for a in groups do
       for c in a.ZombieFollowers do
	     select c]
    let groupleader = 
      [for a in groups do
       where a.ZombieLeader.IsSome
       let z = a.ZombieLeader.Value
       select z]
    let group = groupleader @ zombiegroup
    if group.Count > 0 then
      yield group @ Zombies
  
  rule Zombies = 
    let zmbies = 
      [for a in Zombies do
       where (a.Destroyed = false)
       select a]
    //Debug.Log("zombies count = "+zmbies.Count)
    yield zmbies

  rule Health = 
    if ActiveBoR = "Medipack Red" then
      yield Health + 20.0f
      Debug.Log(Health)

  rule ActiveBoR = 
    if ActiveBoR = "Crossed Wrenches Red" then
      let resourcelist = ["Medipack Red";"Battery Black";"Jerry Can Green"]
      let rnd = Random.Range(0,3)
      yield resourcelist.[rnd]
  
  rule ActiveBoR = 
    let a = 
      [for a in Landscapes do
       for b in a.PickUps do
       for c in b.BonusAndResources do
       where c.Active
       select (c.NameOfBoR)]
    if a.Count > 0 then
      Debug.Log(a.Head)
      yield a.Head
      yield ""

  rule Landscapes, Counter, Gasstations  = 
    let ls = 
      [for a in Landscapes do
       select a]
    let gs = 
      [for b in Gasstations do
       select b]
    if (ls.Count > 0) then
      let randr = Random.Range(1,5) 
      if gs.Count > 0 then 
        let Headpos = if (ls.Head.Position.z < gs.Head.Position.z) then ls.Head.Position else gs.Head.Position
        if Counter > 7 then
          yield Landscapes, 0, new Gasstation(new Vector3(0.0f,0.0f, Headpos.z - 80.0f)) :: Gasstations
          wait (Vector3.Distance(Headpos, Jeep.Value.Position) < 200.0f)
        else
          yield (new Landscape(new Vector3(0.0f,0.0f,Headpos.z - 80.0f), randr)) :: Landscapes, Counter + 1, Gasstations
          yield Landscapes, Counter, Gasstations
          wait (Vector3.Distance(Headpos, Jeep.Value.Position) < 200.0f)
      else 
        let Headpos = ls.Head.Position
        if Counter > 7 then
          yield Landscapes, 0, new Gasstation(new Vector3(0.0f,0.0f, Headpos.z - 80.0f)) :: Gasstations
          wait (Vector3.Distance(Headpos, Jeep.Value.Position) < 200.0f)
        else
          yield (new Landscape(new Vector3(0.0f,0.0f,Headpos.z - 80.0f), randr)) :: Landscapes, Counter + 1, Gasstations
          yield Landscapes, Counter, Gasstations
          wait (Vector3.Distance(Headpos, Jeep.Value.Position) < 200.0f)

  rule Pistols, Flashs = 
    wait Jeep.Value.Active
    yield [new Gun()], [new Light()]
    wait not Jeep.Value.Active
    yield [] , []

  Create() =
    let truk = new Truck("truck1", new Vector3(0.0f,-6.0f,0.0f), false)
    {
     Landscapes = [new Landscape(new Vector3(0.0f,0.0f,-160.0f), 1); new Landscape((Vector3.zero), (2))] 
     Jeep = Some truk
     Zombies = []
     Pistols = []
     Flashs = []
     GUIpanel = new GUI()
     Health = 100.0f
     ActiveBoR = ""
     Counter = 3
     Gasstations = [new Gasstation(new Vector3(0.0f,0.0f, -80.0f))]
    }
}
entity Gasstation = {
  inherit UnityGasstation
  ParkingSpotPositions : [Vector3]
  ParkedJeep : Option<Truck>
  PJpos : Vector3
  Updating : bool

  rule world.Jeep.Value.Active = 
    wait Updating
    wait not Updating
    yield false
    yield true

  rule world.Jeep, Updating = 
    if ParkedJeep.IsNone then
      let pos = new Vector3((PJpos.x - 5.0f),PJpos.y, PJpos.z)
      let z = new Truck("truck1", pos, false)
      yield Some z, true
      yield world.Jeep, false
      wait false
      
  rule ParkedJeep.Value.Destroyed, ParkedJeep = 
    if world.Jeep.IsNone then 
      yield true, None

  rule world.Jeep.Value.Destroyed, world.Jeep, Updating = 
    if world.Jeep.IsSome then
      wait 3.0f
      wait world.Jeep.Value.CollisionWithModel
      yield true, None, true
     
  rule ParkedJeep, ParkingSpotPositions, PJpos = 
    wait ParkingSpotPositions.Count > 0
    let z = 
      [for a in Jeeps do
       select a]
    let PSPH = ParkingSpotPositions.Head 
    let PSPH2 = new Vector3(PSPH.x,PSPH.y,(PSPH.z + 32.5f))
    let p = (new Truck(z.Head + "model", PSPH2, true))
    yield Some p, ParkingSpotPositions, PSPH2
    yield ParkedJeep, [], PSPH2

  rule ParkingSpotPositions = 
    let z = 
      [for a in ParkingSpotList do
       select (a.position)]
    yield z
    wait false

  Create(ps : Vector3) = {
  Base = UnityGasstation.Instantiate(ps)
  ParkingSpotPositions = []
  ParkedJeep = None
  PJpos = Vector3.zero
  Updating = false
  }

}

entity GroupZombie = { 
  inherit UnityGroup
  ZombieFollowers : [Zombie]
  ZombieLeader : Option<Zombie>

  rule ZombieLeader = 
    let leader = new Zombie(U_ZombieLeader)
    yield Some leader
    yield None
    wait false

  rule ZombieFollowers = 
    let z = 
      [for a in U_Zombies do
       select (new Zombie(a))] 
    yield z
    yield []
    wait false

  Create(sps : Vector3) =
  {
    Base = UnityGroup.Instantiate(sps)
    ZombieFollowers = []
    ZombieLeader = None
  }
}

entity ControllerRazor= { // one hand
  inherit SixenseHand
  
  rule Shot = 
    if (Trigger || Input.GetMouseButtonDown(0) && (world.Pistols.Head.InMagazine > 0 && not (world.Pistols.Head.Reloading))) then
      yield true
      yield false

  rule Trigger =
    yield Trigger

  rule Bumper =
    yield Bumper

  Create(joystickName : string) = 
  {
    Base = SixenseHand.Instantiate(joystickName)
  }
}

entity Gun = { //pistol
  GunController : ControllerRazor
  NotInMagazine : int
  Reloading : bool
  ReloadDuration : float
  InMagazine : int
  MagazineSize : int

  rule InMagazine = 
    //Debug.Log("InMagazine left = "+ InMagazine)
    if GunController.Shot && InMagazine > 0 && (Reloading = false) then
      yield InMagazine - 1
      wait (not GunController.Shot) 

  rule NotInMagazine = 
    if world.ActiveBoR = "AmmoBox" then 
      yield NotInMagazine + 50
      Debug.Log("NotInMagazine = "+ NotInMagazine)

  rule Reloading, NotInMagazine, InMagazine = 
    if ((Input.GetKey(KeyCode.R) || InMagazine = 0) && NotInMagazine > 0) then
      yield true, NotInMagazine, InMagazine
      wait (ReloadDuration)
      Debug.Log("notinmag = "+NotInMagazine + " inmag = " + InMagazine)
      if MagazineSize > NotInMagazine then // clip can't be fulled completely
        yield false, 0, NotInMagazine
      else 
        let changed = (MagazineSize - InMagazine)
        Debug.Log("changed = "+changed)
        yield false, NotInMagazine - changed, MagazineSize // fill clip completely

  rule world.Jeep.Value.InMag, world.Jeep.Value.NotInMag = 
     wait Reloading
     wait not Reloading
     yield InMagazine, NotInMagazine

  rule world.Jeep.Value.InMag, world.Jeep.Value.NotInMag = 
     let z = 
       [for a in world.Gasstations do
        where a.Updating
        select a]
     if z.Count = 0 then
       yield InMagazine, NotInMagazine
       wait GunController.Shot

  rule Reloading = 
    if Input.GetKey(KeyCode.C) then
      yield false

  Create() = 
  {
    GunController = new ControllerRazor("Hydra1 - Right")//righthand
    NotInMagazine = 200 //amount of bullets
    Reloading = false
    MagazineSize = 90
    InMagazine = 20
    ReloadDuration = 4.0f
  }
}

entity Light = { //flashlight
  LightController : ControllerRazor
  Battery : float
  Active : bool
  
  rule Active =
    yield LightController.Trigger

  rule Battery = 
     if world.ActiveBoR = "Battery Black" then 
       Debug.Log("Battery = "+Battery)
       yield Battery + 50.0f
    
  rule Battery = 
    //Debug.Log("Battery: " + Battery)
    if Active && Battery > 0.49f then
      yield Battery - 0.5f
    else
      yield Battery

  Create() = 
  {
    LightController = new ControllerRazor("Hydra1 - Left")//lefthand
    Battery = 100.0f
    Active = false
  }
}

entity GUI = { //panel. Each wall contains the same info?
  inherit GUIBarScript

  rule Value =
    yield world.Jeep.Value.CarHP2

  Create() = 
  {
    Base = GUIBarScript.Find()
  }

}

entity Landscape = { //here are the zombies created and destroyed after a frame
  inherit UnityLandscape
  Group : [GroupZombie]
  PickUps : [PickUp]
  Spawnpoints : [Transform]

  rule Group, PickUps, Spawnpoints = // spawning multiple groups on a single landscape
    wait Spawnpoints.Count > 0
    let random_spawnp = Random.Range(0,4) 
    let random_pickup = Random.Range(0,4)
    if random_spawnp <> random_pickup then
      let sps = Spawnpoints.[random_spawnp]
      let sps_pos = sps.position
      let rpu = Spawnpoints.[random_pickup]
      let rpu_pos = rpu.position
      yield [new GroupZombie(sps_pos)], [new PickUp(rpu_pos)], []
      wait false
    else 
      let sps1 = Spawnpoints.[1]
      let sps_pos1 = sps1.position
      let rpu1 = Spawnpoints.[2]
      let rpu_pos1 = rpu1.position
      yield [new GroupZombie(sps_pos1)], [new PickUp(rpu_pos1)], []
      wait false

  rule Spawnpoints = 
    let sps = //[Transform of each Spawnpoint]
      [for a in Spawnpoints2 do
       select a]
    yield sps
    wait false

  Create(pos : Vector3, ps : int) =
  {
  Base = UnityLandscape.Instantiate(pos, ps)
  Spawnpoints = []
  Group = []
  PickUps = []
  }
}

entity PickUp = {
  inherit UnityPickUp
  BonusAndResources : [BonusAndResource]
  BARpos : Vector3

  rule BonusAndResources = 
    let amount = 
      [for a in BonusAndResources do
       where (a.Destroyed = false)
       select a]
    yield amount

  rule BonusAndResources = 
    wait Shuffled.Count > 0
    let p0 = BARpos
    let p1 = BARpos - new Vector3(2.0f,0.0f,0.0f)
    let p2 = BARpos + new Vector3(2.0f,0.0f,0.0f)
    let p3 = BARpos - new Vector3(4.0f,0.0f,0.0f)
    let plist = [p0;p1;p2;p3]
    let Slist = Shuffled
    let BAR = 
      [for a in [0..3] do
       select (new BonusAndResource(plist.[a], Slist.[a]))]
    yield BAR
    wait false

  rule Shuffled = 
    let BARlist = ["AmmoBox";"Crossed Wrenches Red";"Medipack Red";"Battery Black";"Jerry Can Green";"Lightning Blue";"Arrows Green";"Bomb Red";"Shield Metal";"Star Red"]
    yield BARlist
    wait false

  Create(pos : Vector3) = 
  {
    BARpos = pos
    BonusAndResources = []
  }
}
entity BonusAndResource = {
  inherit UnityBonusResource
  NameOfBoR : string
  Active : bool

  rule Active, Destroyed = 
    if collids then 
      yield true, false
      yield true, true

  Create(pos : Vector3, bonus : string) = 
  {
    Base = UnityBonusResource.Instantiate(pos,bonus)
    NameOfBoR = bonus
    Active = false;
  }
}
entity Truck = {
   inherit TruckScript
   Fuel : float
   AxleInfos : [AxleInfo]
   maxMotorTorque : float
   maxSteeringAngle : float
   cnvAccel : float // has to be a value between 0.0f and 1.0f
   JRotation : float // has to be a value between -1.0f and 1.0f
   Keyboard : bool
   isModel : bool
   Active : bool

   rule HeadlightLeftOn, HeadlightRightOn = 
     let z = 
       [for a in world.Gasstations do
        where a.Updating
        select a]
     if z.Count = 0 then
       if world.ActiveBoR = "Lightning Blue" then
         yield true, true
         wait 5.0f
         yield false, false
         wait 0.2f
         yield true, true
         wait 0.1f
         yield false, false
         wait 0.3f
         yield true, true
         wait 0.1f
         yield false, false
   
   rule MagazineGUI = 
     if world.Pistols.Count > 0 then
       if world.Pistols.Head.GunController.Shot then 
         yield ""

   rule CenterOfMass =
     let z = 
       [for a in world.Gasstations do
        where a.Updating
        select a]
     if z.Count = 0 then
       yield new Vector3(1.0f,0.0f,-1.5f)
       wait false

   rule Keyboard = 
      wait Input.GetKeyDown(KeyCode.P)
      yield not Keyboard

   rule cnvAccel = 
      if Keyboard then
        yield Input.GetAxis("Vertical")
      else
        if Acceleration <> 0.0f then 
          yield Acceleration
        else
          yield BrakeAndReverse * -1.0f

   rule JRotation = 
      if Keyboard then
        yield Input.GetAxis("Horizontal")
      else
        yield Steering

   rule Fuel = 
     if world.ActiveBoR = "Jerry Can Green" then 
       Debug.Log("Fuel = "+Fuel)
       yield Fuel + 2000.0f

   rule Active = 
     yield true
     wait false

   rule AxleInfos = 
     if isModel then 
       yield []
       wait false
     else 
       yield [new AxleInfo(FrontLeftWheel,FrontRightWheel,true,true);new AxleInfo(RearLeftWheel,RearRightWheel,true,false)]
       wait false

   rule AxleInfos = 
     let p = 
       [for a in AxleInfos do
        select a]
     let z = 
       [for a in world.Gasstations do
        where a.Updating
        select a]
     if z.Count > 0 then
       yield []

   Create(nm : string, pos : Vector3, ModelIs : bool) = 
    {
      Active = false
      isModel = ModelIs
      Base = TruckScript.Instantiate(nm, pos)
      maxMotorTorque = 250.0f
      maxSteeringAngle = 50.0f
      AxleInfos = []
      Fuel = 4000.0f // gas liters
      cnvAccel = 0.0f
      JRotation = 0.0f
      Keyboard = false;
    }
}

entity AxleInfo = {

  leftWheel : WheelCollider
  rightWheel : WheelCollider
  motor : bool
  steering : bool

  rule steering = 
    yield steering

  rule leftWheel.steerAngle, rightWheel.steerAngle =
    wait steering
    let z = 
       [for a in world.Gasstations do
        where a.Updating
        select a]
    if z.Count = 0 then
      let steeringAngle = world.Jeep.Value.maxSteeringAngle * world.Jeep.Value.JRotation
      if world.Jeep.Value.cnvAccel > 0.0f then
        if ((steeringAngle > 0.001f  && (world.Jeep.Value.RotationY > 30.0f  && world.Jeep.Value.RotationY < 120.0f)) || 
            (steeringAngle < -0.001f && (world.Jeep.Value.RotationY > 200.0f && world.Jeep.Value.RotationY < 330.0f))) then 
          yield 0.0f,0.0f
        else
          yield steeringAngle, steeringAngle
      if world.Jeep.Value.cnvAccel < 0.0f then
        if ((steeringAngle > 0.001f && (world.Jeep.Value.RotationY > 200.0f && world.Jeep.Value.RotationY < 330.0f)) || 
            (steeringAngle < -0.001f && (world.Jeep.Value.RotationY > 30.0f && world.Jeep.Value.RotationY < 120.0f))) then 
          yield 0.0f,0.0f
        else
          yield steeringAngle, steeringAngle

  rule leftWheel.motorTorque, rightWheel.motorTorque, world.Jeep.Value.Fuel =
    let z = 
       [for a in world.Gasstations do
        where a.Updating
        select a]
    if z.Count = 0 then
      if world.Jeep.Value.Fuel > 0.99f then
        if ((world.Jeep.Value.cnvAccel <> 0.0f) && (leftWheel.isGrounded || rightWheel.isGrounded)) then
          let dir = world.Jeep.Value.cnvAccel
          let speed = world.Jeep.Value.maxMotorTorque * world.Jeep.Value.cnvAccel * -1.0f
          if (world.ActiveBoR = "Arrows Green") then 
            yield leftWheel.motorTorque, rightWheel.motorTorque, world.Jeep.Value.Fuel
            .| (world.Jeep.Value.cnvAccel <> dir) =>
              yield speed, speed, world.Jeep.Value.Fuel - 1.0f
            .| _ =>
              yield speed * 10.0f, speed * 10.0f, world.Jeep.Value.Fuel
              wait 2.0f
          else
            yield speed, speed, world.Jeep.Value.Fuel - 1.0f
        else
          yield 0.0f,0.0f, world.Jeep.Value.Fuel
      else
        yield 0.0f,0.0f,0.0f

  Create(lW : WheelCollider, rW : WheelCollider, m : bool, s: bool)=
  {
    leftWheel = lW
    rightWheel = rW
    motor = m
    steering = s
  }
}

entity Zombie = {
  inherit UnityZombie
  Life : float

  rule Destroyed = 
    wait dead2
    wait 0.8f
    yield true

  rule Life, CollisionDamage =
    if (CollisionDamage <> 0.0f) then
      Debug.Log("Collided zombie HP before:" + Life)
      yield Life - (CollisionDamage * 4.0f), 0.0f
      Debug.Log("Collided zombie HP after:" + Life)

  rule CollisionDamage =
    yield CollisionDamage

  rule Life = 
    wait world.Jeep.IsSome
    if (world.Jeep.Value.Active) then
      if (OnMouseOver && (world.Pistols.Head.GunController.Trigger || Input.GetMouseButtonDown(0))) then
        if Life > 0.49f then 
          yield Life - 40.0f
        else
	        yield 0.0f

  rule shot, dead2 = 
    if Life < 0.0f then
      yield true, true

  Create(trans : Transform) = 
  {
  Base = UnityZombie.Find(trans)
  Life = 100.0f // healthpoints
  }
}